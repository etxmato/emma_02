
10 CLS: PRINT TAB(7);"REGRESSION ANALYSIS": PRINT : PRINT : PRINT 
20 REM
30 REM  *INITIALIZE VARIABLES*
40 REM
50 N=1:U=0:V=U:W1=U:W2=U:W3=U:W4=U:W5=U:W6=U:W7=U
60 X1=U:X2=U:X3=U:X4=U:Y1=U:Y2=U:Z1=U:Z2=U:Z3=U:Z4=U
70 REM
80 REM  *ENTER DATA POINTS*
90 REM
100 PRINT "ENTER DATA POINTS:"
110 P=1: PRINT "POINT ";N;: INPUT X,Y
120 INPUT "ENTER"A$:T=ASC(A$)
130 IF T=#4E GOTO 110
140 IF T=#44 LET N=N-1: GOTO 300
150 IF T=#43 IF N>1 LET P=-1: GOTO 200
160 IF T<>#0D IF T<>#59 GOTO 120
170 REM
180 REM  *ADJUST SUMS*
190 REM
200 N=N+P:X1=X1+X*P:X2=X2+X*X*P:X3=X3+X*X*X*P:X4=X4+X*X*X*X*P
210 Y1=Y1+Y*P:Y2=Y2+Y*Y*P
220 Z1=Z1+X*Y*P:Z2=Z2+X*X*Y*P:Z3=Z3+X*Y*Y*P:Z4=Z4+X*X*Y*Y*P
230 IF X>0 LET W1=W1+LOG(X)*P:W3=W3+LOG(X)*LOG(X)*P:W7=W7+Y*LOG(X)*P:U=U+P
240 IF Y>0 LET W2=W2+LOG(Y)*P:W4=W4+LOG(Y)*LOG(Y)*P:W6=W6+X*LOG(Y)*P:V=V+P
250 IF X>0 IF Y>0 LET W5=W5+LOG(X)*LOG(Y)*P
260 GOTO 110
270 REM
280 REM  *DETERMINE BEST FIT*
290 REM
300 PRINT : PRINT : INPUT "QUADRATIC REGRESSION"A$: IF ASC(A$)=#59 GOSUB 1060:O=5: GOTO 400
310 INPUT "HYPERBOLIC REGRESSION"A$: IF ASC(A$)=#59 GOSUB 1130:O=6: GOTO 400
320 T=1: IF N=U IF N=V LET T=4
330 S=0: RESTORE: FOR L=1 TO T
340 READ A,B,C
350 IF ABS(C)>=ABS(S) LET O=L:Q=A:R=B:S=C
360 NEXT 
370 REM
380 REM  *PERFORM COMMAND*
390 REM
400 CLS: PRINT "MENU:": PRINT " 0 - EXIT": PRINT " 1 - STATISTICS": PRINT " 2 - EQUATION"
410 PRINT " 3 - POINT COMPUTATION": PRINT " 4 - ADDITIONAL DATA"
420 PRINT : INPUT "YOUR SELECTION"M: IF M*(M-4)>0 GOTO 420
430 CLS: IF M=0 END
440 IF M=1 GOSUB 530
450 IF M=2 GOSUB 650
460 IF M=3 GOSUB 900
470 IF M=4 GOSUB 1020
480 PRINT : INPUT "AGAIN"A$: IF ASC(A$)=#59 PRINT : GOTO 440
490 GOTO 400
500 REM
510 REM  *GIVE STATISTICS*
520 REM
530 PRINT "INFORMATION ON 'X':"
540 PRINT " MEAN          = ";X1/N
550 PRINT " STD DEV [N]   = ";SQR((X2-X1*X1/N)/N)
560 PRINT " STD DEV [N-1] = ";SQR((X2-X1*X1/N)/(N-1))
570 PRINT : PRINT "INFORMATION ON 'Y':"
580 PRINT " MEAN          = ";Y1/N
590 PRINT " STD DEV [N]   = ";SQR((Y2-Y1*Y1/N)/N)
600 PRINT " STD DEV [N-1] = ";SQR((Y2-Y1*Y1/N)/(N-1))
610 RETURN 
620 REM
630 REM  *PRINT EQUATION*
640 REM
650 PRINT "Y = ";
660 IF O<>1 GOTO 690
670 PRINT Q;"*X + ";: IF R<0 PRINT CHR$(8,8);"- ";
680 PRINT ABS(R): GOTO 850
690 IF O=2 PRINT R;"*X^(";Q;")": GOTO 850
700 IF O=3 PRINT R;"*E^(";Q;"*X)": GOTO 850
710 IF O<>4 GOTO 740
720 PRINT R;" + ";: IF Q<0 PRINT CHR$(8,8);"- ";
730 PRINT ABS(Q);"*LN(X)": GOTO 850
740 IF O<>5 GOTO 780
750 PRINT C;"*X^2 + ";: IF B<0 PRINT CHR$(8,8);"- ";
760 PRINT ABS(B);"*X + ";: IF C<0 PRINT CHR$(8,8);"- ";
770 PRINT ABS(A): RETURN 
780 IF Q=0 PRINT "F(X)":S=1: GOTO 850
790 IF A=0 PRINT B;"*X";: GOTO 820
800 PRINT "(";A;" + ";: IF B<0 PRINT CHR$(8,8);"- ";
810 PRINT ABS(B);"*X)";
820 IF C=0 LET S=1: GOTO 850
830 PRINT "/": PRINT "    (1 + ";: IF C<0 PRINT CHR$(8,8);"- ";
840 PRINT ABS(C);"*X)": RETURN 
850 PRINT : PRINT "CORRELATION = ";S
860 RETURN 
870 REM
880 REM  *GIVE APPROXIMATIONS*
890 REM
900 INPUT "COORDINATE"T
910 IF O=1 LET X=(T-R)/Q:Y=Q*T-R: GOTO 970
920 IF O=2 LET X=(T/R)^(1/Q):Y=R*T^Q: GOTO 970
930 IF O=3 LET X=LOG(T/R)/Q:Y=R*EXP(Q*T): GOTO 970
940 IF O=4 LET X=EXP((T-R)/Q):Y=R+Q*LOG(T): GOTO 970
950 IF O=5 LET X=0:Y=C*T*T+B*T+A: GOTO 970
960 X=0:Y=(A+B*T)/(1+C*T): IF Q=0 PRINT "PERFECT CORRELATION": RETURN 
970 PRINT "X = ";X: PRINT "Y = ";Y
980 RETURN 
990 REM
1000 REM *RETURN FOR MORE DATA*
1010 REM
1020 N=N+1: GOTO 110
1030 REM
1040 REM *QUADRATIC*
1050 REM
1060 Q=N*X2-X1*X1:R=N*X3-X1*X2:S=N*Z1-X1*Y1
1070 C=1/(Q*(N*X4-X2*X2)-R*R)*(Q*(N*Z2-X2*Y1)-R*S)
1080 B=(S-R*C)/Q:A=(Y1-B*X1-C*X2)/N
1090 RETURN 
1100 REM
1110 REM *HYPERBOLIC*
1120 REM
1130 Q=(Z2*Z2-X2*Z4)*N+(X1*Z4-Z1*Z2)*X1+(X2*Z1-X1*Z2)*Z1
1140 IF Q=0 LET C=Q: RETURN 
1150 A=((Z1*Z3-Y1*Z4)*X2+(X1*Z4-Z1*Z2)*Z1+(Y1*Z2-X1*Z3)*Z2)/Q
1160 B=((Z2*Z3-Z1*Z4)*N+(Y1*Z4-Z1*Z3)*X1+(Z1*Z1-Z2*Y1)*Z1)/Q
1170 C=((X2*Z3-Z1*Z2)*N+(Y1*Z2-X1*Z3)*X1+(X1*Z1-X2*Y1)*Z1)/Q
1180 RETURN 
1190 END
1200 REM
1210 REM  *COEFFICIENTS*
1220 REM
1230 REM  LINEAR LEAST-SQUARES
1240 DATA (X1*Y1/N-Z1)/(X1*X1/N-X2)
1250 DATA (Y1-A*X1)/N
1260 DATA A*(X1*Y1/N-Z1)/(Y2-Y1*Y1/N)
1270 REM
1280 REM  POWER CURVE
1290 DATA (W5-W1*W2/N)/(W3-W1*W1/N)
1300 DATA EXP((W2-A*W1)/N)
1310 DATA (W5-W1*W2/N)*(W5-W1*W2/N)/(W3-W1*W1/N)/(W4-W2*W2/N)
1320 REM
1330 REM  EXPONENTIAL CURVE
1340 DATA (W6-X1*W2/N)/(X2-X1*X1/N)
1350 DATA EXP((W2-A*X1)/N)
1360 DATA (W6-X1*W2/N)*(W6-X1*W2/N)/(X2-X1*X1/N)/(W4-W2*W2/N)
1370 REM
1380 REM  LOGARITHMIC CURVE
1390 DATA (W7-W1*Y1/N)/(W3-W3/N)
1400 DATA (Y1-A*W1)/N
1410 DATA (W7-W1*Y1/N)*(W7-W1*Y1/N)/(W3-W3/N)/(Y2-Y1*Y1/N)
1420 REM
1430 REM
1440 REM
1450 REM  *VARIABLES*
1460 REM
1470 REM  A,B,C=TEMP. COEFF.
1480 REM  N=NUMBER OF POINTS
1490 REM  O=TYPE OF CURVE
1500 REM  P=+VE OR -VE
1510 REM  Q,R,S=BEST COEFF.
1520 REM  T=TEMPORARY
1530 REM  U,V=LOG FLAGS
1540 REM  W1=LOG(X)
1550 REM  W2=LOG(Y)
1560 REM  W3=(LOG(X))^2
1570 REM  W4=(LOG(Y))^2
1580 REM  W5=LOG(X)*LOG(Y)
1590 REM  W6=X*LOG(Y)
1600 REM  W7=Y*LOG(X)
1610 REM  X1=X,X2=X^2,X3=X^3
1620 REM  X4=X^4,Y1=Y,Y2=Y^2
1630 REM  Z1=X*Y,Z2=X^2*Y
1640 REM  Z3=X*Y^2,Z4=X^2*Y^2

