
10 REM  *SEA BATTLE*
20 REM  FROM:  MORE BASIC COM-
30 REM         PUTER GAMES, 143
40 REM
50 REM
60 REM  *INITIALIZATION*
70 REM
80 CLS: PRINT TAB(11);"SEA BATTLE": PRINT : PRINT : PRINT 
90 PRINT "PRESS 'I' TO INITIALIZE..."
100 X=RND: IF INP(7)<>73 IF INP(7)<>105 GOTO 100
110 PRINT : PRINT "--------------------------------"
120 DIM A(20,20),B(5),C(16),D(9)
130 DEFINT B: RESTORE
140 REM  OCEAN ARRAY
150 FOR A=1 TO 20: FOR B=1 TO 20:A(A,B)=0: NEXT : NEXT 
160 REM  ISLAND ARRAY
170 FOR A=7 TO 13: FOR B=7 TO 12: READ A(A,B): NEXT : NEXT 
180 DATA 0,1,1,1,0,0,0,1,1,1,1,0,1,1,1,0,1,1,1,1,0,0,0,1
190 DATA 1,1,0,0,1,1,0,1,1,0,1,0,0,0,1,0,0,0
200 REM  COURSE ARRAY
210 FOR A=1 TO 16: READ C(A): NEXT 
220 DATA -1,0,-1,1,0,1,1,1,1,0,1,-1,0,-1,-1,-1
230 REM  MAP ELEMENTS
240 FOR A=1 TO 7: READ M$(A): NEXT 
250 DATA " ","*","X","S","H","#","$"
260 REM  SUBMARINE
270 S1=10:S2=10:A(S1,S2)=2
280 REM  ENEMY SHIPS
290 S=INT(RND*16)+15:X=(INT(RND*4)+1)*2-1:D8=C(X*2-1):D9=C(X*2)
300 FOR A=1 TO S
310 X1=INT(RND*20)+1:X2=INT(RND*20)+1: IF A(X1,X2)<>0 GOTO 310
320 A(X1,X2)=3: NEXT : PRINT "TRY TO DESTROY ";S;" ENEMY SHIPS"
330 PRINT "BEFORE THEY DESTROY YOU."
340 REM  HEADQUARTERS
350 S3=INT(RND*20)+1:S4=INT(RND*20)+1: IF A(S3,S4)<>0 GOTO 350
360 A(S3,S4)=4
370 REM  UNDERWATER MINES
380 FOR A=1 TO INT(RND*8)+8
390 X1=INT(RND*20)+1:X2=INT(RND*20)+1: IF A(X1,X2)<>0 GOTO 390
400 A(X1,X2)=5: NEXT 
410 REM  SEA MONSTERS
420 FOR A=1 TO 4
430 X1=INT(RND*18)+2:X2=INT(RND*18)+2: IF A(X1,X2)<>0 GOTO 430
440 A(X1,X2)=6:Y=INT(RND*8)+1:M1=C(Y*2-1):M2=C(Y*2): NEXT 
450 REM  STARTING VALUES
460 FOR A=1 TO 9:D(A)=0: NEXT :C=30:P=6000:F=2500:T=10:M=3:D=100:D2=2
470 REM
480 REM  *PROCESS COMMAND*
490 REM
500 PRINT : PRINT "================================": INPUT "YOUR COMMAND"O: PRINT 
510 IF O=0 GOTO 760
520 IF O=1 GOTO 1170
530 IF O=2 GOTO 1540
540 IF O=3 GOTO 1830
550 IF O=4 GOTO 2140
560 IF O=5 GOTO 2220
570 IF O=6 GOTO 2410
580 IF O=7 GOTO 2480
590 IF O=8 GOTO 2690
600 IF O=9 GOTO 2810
610 PRINT "COMMANDS:"
620 PRINT "  0 - NAVIGATION"
630 PRINT "  1 - SONAR"
640 PRINT "  2 - TORPEDO CONTROL"
650 PRINT "  3 - MISSILE CONTROL"
660 PRINT "  4 - MANEUVERING"
670 PRINT "  5 - STATUS REPORTING"
680 PRINT "  6 - HEADQUARTERS"
690 PRINT "  7 - SABOTAGE"
700 PRINT "  8 - POWER CONVERSION"
710 PRINT "  9 - SURRENDER"
720 GOTO 500
730 REM
740 REM  *NAVIGATION*
750 REM
760 IF D(1)<0 PRINT "ENGINES ARE UNDER REPAIR": GOTO 500
770 IF C<9 PRINT "CREW TOO SMALL": GOTO 500
780 D1=1: IF D<=50 LET D1=D1-.23+RND/10
790 GOSUB 3680
800 PRINT P;: INPUT " UNITS POWER AVAILABLE"P1: IF P*(P-P1)<0 GOTO 800
810 IF P1>1000 IF RND>=.43 PRINT "REACTOR GOES SUPERCRITICAL!": GOTO 3790
820 X=S1:Y=S2:Q1=1: FOR A=1 TO INT(INT(P1/100+.5)*D1+.5)
830 IF X+X1>0 IF X+X1<21 IF Y+Y1>0 IF Y+Y1<21 GOTO 850
840 PRINT "EXIT FROM AREA FORBIDDEN": GOTO 940
850 I=A(X+X1,Y+Y1)
860 IF I=1 GOTO 930
870 IF I=2 EXIT 1110
880 IF I=3 GOTO 960
890 IF I=4 GOTO 990
900 IF I=5 GOTO 1010
910 IF I=6 GOTO 1020
920 X=X+X1:Y=Y+Y1:P=P-100: GOTO 1040
930 PRINT "ALMOST AGROUND"
940 A(X,Y)=2:A(S1,S2)=0:S1=X:S2=Y: GOTO 2850
950 GOTO 1110
960 IF D>50 GOTO 920
970 PRINT "SUB RAMS SHIP!":S=S-1: IF S=0 GOTO 3820
980 GOTO 3790
990 IF D>50 GOTO 920
1000 PRINT "SUB RAMS HEADQUARTERS!": GOTO 3790
1010 PRINT "SUB HITS MINE!": GOTO 3790
1020 IF RND<.21 GOTO 1110
1030 PRINT "SUB EATEN BY SEA MONSTER!": GOTO 3790
1040 FOR X3=X-2 TO X+2: FOR Y3=Y-2 TO Y+2: IF X3>0 IF X3<=20 IF Y3>0 IF Y3<=20 GOTO 1060
1050 GOTO 1100
1060 IF A(X,Y)<>6 GOTO 1100
1070 IF RND<.25 GOTO 1030
1080 IF Q1=0 GOTO 1100
1090 PRINT "NARROW ESCAPE FROM MONSTER":Q1=0
1100 NEXT : NEXT 
1110 NEXT 
1120 IF P<=0 PRINT "REACTOR FUEL EXPENDED!": GOTO 3390
1130 GOTO 940
1140 REM
1150 REM  *SONAR*
1160 REM
1170 IF D(2)<0 PRINT "SONAR IS UNDER REPAIR": GOTO 500
1180 IF C<6 PRINT "CREW TOO SMALL": GOTO 500
1190 INPUT "OPTION"O: IF O=1 GOTO 1350
1200 IF O=2 GOTO 1420
1210 IF O<>0 GOTO 1190
1220 REM  MAP PRINTOUT
1230 Y1=1:Y2=20: FOR A=1 TO 15: GOSUB 1270: NEXT : FOR A=1 TO 15: PRINT TAB(6);A$(A): NEXT 
1240 FOR A=16 TO 20: GOSUB 1270: NEXT : FOR A=16 TO 20: PRINT TAB(6);A$(A): NEXT 
1250 P=P-50: IF P>0 GOTO 500
1260 GOTO 1120
1270 A$(A)="": FOR B=Y1 TO Y2: IF A(A,B)<>0 GOTO 1300
1280 IF A<>1 IF A<>20 IF B<>1 IF B<>20 GOTO 1300
1290 A$(A)=A$(A)+".": GOTO 1330
1300 I=A(A,B)+1: IF D<50 IF RND>.23 IF I<>2 IF I<>3 GOTO 1290
1310 IF RND<.15 IF I>3 GOTO 1290
1320 A$(A)=A$(A)+M$(I)
1330 NEXT : RETURN 
1340 REM  SELECTIVE MAP
1350 PRINT : INPUT "Y-COORDINATES"X1,X2: INPUT "X-COORDINATES"Y1,Y2
1360 IF X1>0 IF X1<21 IF X2>0 IF X2<21 IF X1<X2 IF Y1>0 IF Y1<21 IF Y2>0 IF Y2<21 IF Y1<Y2 GOTO 1380
1370 GOTO 1350
1380 PRINT : FOR A=X1 TO X2: GOSUB 1270: PRINT TAB(16-(Y2-Y1)/2);A$(A): NEXT 
1390 P=P-INT((X2-X1+1)*(Y2-Y1+1)/8): IF P>0 GOTO 500
1400 GOTO 1120
1410 REM  DIRECTIONAL INFO.
1420 PRINT : FOR A=1 TO 5:B(A)=0: NEXT : PRINT "HEADING SHIPS DISTANCES"
1430 FOR A=1 TO 8:X1=C(A*2-1):Y1=C(A*2):X3=0
1440 FOR B=1 TO 20: IF S1+X1*B>0 IF S1+X1*B<21 IF S2+Y1*B>0 IF S2+Y1*B<21 GOTO 1460
1450 EXIT 1480
1460 IF A(S1+X1*B,S2+Y1*B)=3 LET X3=X3+1:B(X3)=B
1470 NEXT 
1480 IF X3=0 GOTO 1500
1490 PRINT "   ";A,"  ";X3;"   ";: FOR B=1 TO X3: PRINT B(B);",";: NEXT : PRINT CHR$(8)
1500 NEXT : GOTO 1250
1510 REM
1520 REM  *TORPEDO CONTROL*
1530 REM
1540 IF D(3)<0 PRINT "TORPEDO TUBES ARE UNDER REPAIR": GOTO 500
1550 IF C<10 PRINT "CREW TOO SMALL": GOTO 500
1560 IF T=0 PRINT "NO TORPEDOS LEFT": GOTO 500
1570 IF D>=2000 IF RND<.5 PRINT "SUB IMPLODES UPON FIRING!": GOTO 3790
1580 GOSUB 3680:X=S1:Y=S2:I=7-RND*4: IF D>50 LET I=I+5
1590 FOR X2=1 TO INT(I+.5): IF X+X1>0 IF X+X1<21 IF Y+Y1>0 IF Y+Y1<21 GOTO 1640
1600 PRINT "TORPEDO OUT OF SONAR RANGE"
1610 T=T-1:P=P-150
1620 IF P>0 EXIT 2850
1630 EXIT 1130
1640 I=A(X+X1,Y+Y1)
1650 IF I=1 GOTO 1720
1660 IF I=2 GOTO 1790
1670 IF I=3 GOTO 1740
1680 IF I=4 GOTO 1760
1690 IF I=5 GOTO 1770
1700 IF I=6 GOTO 1780
1710 X=X+X1:Y=Y+Y1: PRINT "(";X;",";Y;")": GOTO 1790
1720 PRINT "LAND HIT"
1730 A(X+X1,Y+Y1)=0: GOTO 1610
1740 PRINT TAB(9);"-- SHIP HIT --":S=S-1: IF S>0 GOTO 1730
1750 EXIT 3820
1760 PRINT "HEADQUARTERS DESTROYED":S3=0:S4=0:D2=0: GOTO 1730
1770 PRINT "MINE HIT": GOTO 1730
1780 PRINT "TORPEDO EATEN BY A SEA MONSTER": GOTO 1610
1790 NEXT : PRINT "DUD": GOSUB 1610
1800 REM
1810 REM  *MISSILE CONTROL*
1820 REM
1830 IF D(4)<0 PRINT "MISSILE SILOS ARE UNDER REPAIR": GOTO 500
1840 IF C<24 PRINT "CREW TOO SMALL": GOTO 500
1850 IF M=0 PRINT "NO MISSILES LEFT": GOTO 500
1860 IF D>50 IF D<2000 GOTO 1890
1870 INPUT "RECOMMEND NEW DEPTH...PROCEED"A$: IF MID$(A$,1,1)<>"Y" GOTO 500
1880 IF RND>.5 PRINT "MISSILE EXPLODES UPON FIRING!": GOTO 3790
1890 GOSUB 3680
1900 INPUT "FUEL"F1: IF F1=0 GOTO 500
1910 IF F1*(F-F1)<0 PRINT F;" POUNDS LEFT": GOTO 1900
1920 F2=INT(F1/75+.5)
1930 IF S1+X1*F2>0 IF S1+X1*F2<21 IF S2+Y1*F2>0 IF S2+Y1*F2<21 GOTO 1960
1940 PRINT "MISSILE BEYOND SONAR RANGE"
1950 M=M-1:F=F-F1:P=P-300: GOSUB 1620
1960 D3=0:D4=0:D5=0:D6=0
1970 FOR A=S1+X1*F2-1 TO S1+X1*F2+1: FOR B=S2+Y1*F2-1 TO S2+Y1*F2+1
1980 IF A>0 IF A<21 IF B>0 IF B<21 GOTO 2000
1990 GOTO 2070
2000 I=A(A,B): IF I=3 LET D3=D3+1: GOTO 2060
2010 IF I=6 LET D4=D4+1: GOTO 2060
2020 IF I=5 LET D5=D5+1: GOTO 2060
2030 IF I=1 LET D6=D6+1
2040 IF I=4 PRINT "HEADQUARTERS DESTROYED":D3=0:S4=0:D2=0: GOTO 2060
2050 IF I=2 PRINT "SUB HIT!": GOTO 3750
2060 A(A,B)=0
2070 NEXT : NEXT : IF D6<>0 PRINT "ISLAND HHIT"
2080 IF D5<>0 PRINT "SEVERAL MINES DESTROYED"
2090 IF D4<>0 PRINT D4;" SEA MONSTERS KILLED"
2100 PRINT " -- ";D3;" ENEMY SHIPS DESTROYED --":S=S-D3: GOTO 1950
2110 REM
2120 REM  *MANEUVERING*
2130 REM
2140 IF D(5)<0 PRINT "BALLAST CONTROLS NEED REPAIR": GOTO 500
2150 IF C<13 PRINT "CREW TOO SMALL": GOTO 500
2160 PRINT D;" FEET CURRENT DEPTH": INPUT "NEW DEPTH"D1: IF D1*(D1-2999)>0 PRINT "HULL CRUSHED!": GOTO 3790
2170 X=INT(ABS((D-D1)/2+.5)):P=P-X: PRINT "MANEUVER COMPLETED  <";X;" UNITS>"
2180 D=D1: GOTO 2850
2190 REM
2200 REM  *STATUS CONTROL*
2210 REM
2220 IF D(6)<0 PRINT "NO REPORTS CAN GET THROUGH": GOTO 500
2230 IF C<4 PRINT "NO ONE LEFT TO GIVE THE REPORT": GOTO 500
2240 PRINT S;" ENEMY SHIPS LEFT": PRINT P;" UNITS POWER LEFT": PRINT T;" TORPEDOS LEFT": PRINT M;" MISSILES LEFT"
2250 PRINT C;" CREWMAN LEFT": PRINT F;" POUNDS OF FUEL LEFT": PRINT 
2260 INPUT "DAMAGE REPORT"A$: IF MID$(A$,1,1)<>"Y" GOTO 2370
2270 PRINT "    ITEM";TAB(21);"DAMAGE"
2280 PRINT " ENGINES";TAB(20);D(1)
2290 PRINT " SONAR";TAB(20);D(2)
2300 PRINT " TORPEDO TUBES";TAB(20);D(3)
2310 PRINT " MISSILE SILOS";TAB(20);D(4)
2320 PRINT " MANEUVERING";TAB(20);D(5)
2330 PRINT " STATUS CONTROL";TAB(20);D(6)
2340 PRINT " HEADQUARTERS";TAB(20);D(7)
2350 PRINT " SABOTAGE UNIT";TAB(20);D(8)
2360 PRINT " CONVERTER";TAB(20);D(8)
2370 PRINT : PRINT "SUB AT (";S1;",";S2;")": PRINT "CURRENT DEPTH ";D;" FEET": GOTO 500
2380 REM
2390 REM  *HEADQUARTERS*
2400 REM
2410 IF D(7)<0 PRINT "HEADQUARTERS IS DAMAGED": GOTO 500
2420 IF D2<=0 PRINT "HEADQUARTERS DESERTED": GOTO 500
2430 IF SQR((S1-S3)*(S1-S3)+(S2-S4)*(S2-S4))>2 IF D>50 PRINT "UNABLE TO DOCK": GOTO 500
2440 PRINT "SUPPLIES BROUGHT BY DIVERS":P=4000:T=8:M=2:F=1500:C=25:D2=D2-1: GOTO 2850
2450 REM  
2460 REM  *SABOTAGE UNIT*
2470 REM
2480 IF D(8)<0 PRINT "HATCHES INACCESSIBLE": GOTO 500
2490 IF C<11 PRINT "CREW TOO SMALL": GOTO 500
2500 D3=0:D4=0: FOR A=S1-2 TO S1+2: FOR B=S2-2 TO S2+2
2510 IF A>0 IF A<21 IF B>0 IF B<21 GOTO 2530
2520 GOTO 2550
2530 IF A(A,B)=3 LET D3=D3+1
2540 IF A(A,B)=6 LET D4=D4+1
2550 NEXT : NEXT : IF D3=0 PRINT "NO SHIPS WITHIN RANGE": GOTO 500
2560 PRINT D3;" SHIPS WITHIN RANGE"
2570 INPUT "NUMBER MEN"Q1: IF C-Q1<10 PRINT "AT LEAST 10 MUST REMAIN": GOTO 2570
2580 D5=INT(D3/Q1+.5):D6=0: FOR A=S1-2 TO S1+2: FOR B=S2-2 TO S2+2
2590 IF D3/Q1>1-RND IF RND+D3/Q1<.9 GOTO 2620
2600 IF A(A,B)<>3 GOTO 2620
2610 D6=D6+1:A(A,B)=0:S=S-1: IF S=0 GOTO 3820
2620 NEXT : NEXT : PRINT "    -- ";D6;" SHIPS DESTROYED --":D6=0:D7=0: FOR A=1 TO Q1: IF RND>.6 LET D7=D7+1
2630 NEXT : FOR A=1 TO Q1-D7: IF RND<.15 LET D6=D6+1
2640 NEXT : IF D4<>0 PRINT "A SEA MONSTER EATS ";D7;" MEN":C=C-D7
2650 PRINT D6;" MEN LOST THROUGH ACCIDENTS":C=C-D6:P=P-INT(10*Q1+RND*10): GOTO 2850
2660 REM
2670 REM  *POWER CONVERTER*
2680 REM
2690 IF D(9)<0 PRINT "POWER CONVERTER IS INOPERATIVE": GOTO 500
2700 IF C<6 PRINT "CREW TOO SMALL": GOTO 500
2710 INPUT "OPTION (1=F-P  2=P-F)"O: IF O=2 GOTO 2750
2720 IF O<>1 GOTO 500
2730 PRINT F;" POUNDS FUEL AVAILABLE": INPUT "CONVERT"C1: IF C1*(F-C1)<0 GOTO 2730
2740 F=F-C1:P=P+INT(C1/3): GOTO 2770
2750 PRINT P-1;" UNITS POWER AVAILABLE": INPUT "CONVERT"C1: IF C1*(P-1-C1)<0 GOTO 2750
2760 P=P-C1:F=F+INT(C1*3)
2770 PRINT : PRINT "CONVERSION COMPLETED": PRINT P;" UNITS PWR.  ";F;" LBS. FUEL": GOTO 2850
2780 REM
2790 REM  *SURRENDER*
2800 REM
2810 PRINT "IT'S ABOUT TIME!": GOTO 3790
2820 REM
2830 REM  *RETALIATION*
2840 REM
2850 PRINT : PRINT : PRINT " RESULT OF LAST ENEMY MANEUVERS": PRINT 
2860 Q=0: FOR A=S1-4 TO S1+4: FOR B=S2-4 TO S2+4: IF A>0 IF A<21 IF B>0 IF B<21 GOTO 2880
2870 GOTO 2890
2880 IF A(A,B)=3 LET Q=Q+RND/SQR((S1-A)*(S1-A)+(S2-B)*(S2-B))
2890 NEXT : NEXT : IF Q=0 PRINT "NO SHIPS NEARBY": GOTO 3110
2900 PRINT "DEPTH CHARGES  ";: IF RND<.5 PRINT "<PORT SIDE>": GOTO 2920
2910 PRINT "<STARBOARD SIDE>"
2920 IF Q<=.13 IF RND<=.92 PRINT "NO REAL DAMAGE": GOTO 3110
2930 IF Q<=.36 IF RND<=.96 PRINT "LIGHT, SUPERFICIAL DAMAGE":P=P-50:D(INT(RND*9)+1)=-RND*2: GOTO 3110
2940 IF Q<=.6 IF RND<=.975 GOTO 2960
2950 GOTO 2980
2960 PRINT "MODERATE DAMAGE":P=P-75+INT(RND*30)
2970 FOR A=1 TO 2:B=INT(RND*9)+1:D(B)=D(B)-RND*8: NEXT : GOTO 3110
2980 IF Q<=.9 IF RND<=.983 GOTO 3000
2990 GOTO 3030
3000 PRINT "HEAVY DAMAGE"
3010 P=P-200+INT(RND*76):C=C-INT(RND*C/3)
3020 FOR A=1 TO 4+INT(RND*2):B=INT(RND*9)+1:D(B)=D(B)-RND*11: NEXT : GOTO 3110
3030 PRINT "CRITICAL DAMAGE - HELP NEEDED!"
3040 A$="VRAVUKXCNVPCRHHFDRSAXQURLQTRHXVACVFZYITCBSSYYKDQIPCAEGOGPCNOTSIO"
3050 X=INT(RND*60)+1: PRINT "HERE IS 'HELP' IN CODE: ";MID$(A$,X,4);
3060 WAIT(300)
3070 PRINT CHR$(24)
3080 INPUT "SEND 'HELP' IN CODE"B$: PRINT 
3090 IF B$=MID$(A$,X,4) PRINT "FAST WORK - HELP ARRIVES": GOTO 3010
3100 PRINT "MESSAGE GARBLED - NO HELP!": GOTO 3790
3110 I=0: FOR A=1 TO 9: IF D(A)<0 LET I=I+1
3120 NEXT : IF I=9 PRINT "DAMAGE TOO MUCH!": GOTO 3790
3130 PRINT 
3140 FOR A=1 TO 20: FOR B=1 TO 20: IF A(A,B)<>3 GOTO 3390
3150 W=D8:V=D9: IF A+W>0 IF A+W<21 IF B+V>0 IF B+V<21 GOTO 3180
3160 FOR X=19 TO 1 STEP -1: IF A(A-W*X,B-V*X)=0 LET A(A-W*X,B-V*X)=3:A(A,B)=0: EXIT 3600
3170 NEXT : END
3180 I=A(A+W,B+V)
3190 IF I=1 GOTO 3270
3200 IF I=2 GOTO 3290
3210 IF I=3 GOTO 3270
3220 IF I=4 GOTO 3310
3230 IF I=5 GOTO 3330
3240 IF I=6 GOTO 3350
3250 A(A+W,B+V)=3
3260 A(A,B)=0: GOTO 3600
3270 X=INT(RND*8)+1:W=C(X*2-1):V=C(X*2): IF A+W>0 IF A+W<21 IF B+V>0 IF B+V<21 GOTO 3180
3280 GOTO 3270
3290 IF D>50 GOTO 3270
3300 PRINT "SUB RAMMED BY SHIP!": GOTO 3790
3310 IF RND<.15 GOTO 3270
3320 PRINT "HEADQUARTERS RAMMED":S3=0:S4=0:D2=0:A(A+W,B+V)=0: GOTO 3370
3330 IF RND<.7 GOTO 3270
3340 PRINT "SHIP DESTROYED BY A MINE": GOTO 3370
3350 IF RND<.8 GOTO 3270
3360 PRINT "SHIP EATEN BY A SEA MONSTER"
3370 S=S-1: IF S=0 GOTO 3820
3380 GOTO 3260
3390 IF A(A,B)<>6 GOTO 3600
3400 IF A+M1>0 IF A+M1<21 IF B+M2>0 IF B+M2<21 GOTO 3420
3410 GOTO 3490
3420 I=A(A+M1,B+M2)
3430 IF I=1 GOTO 3490
3440 IF I=2 GOTO 3520
3450 IF I=3 GOTO 3530
3460 IF I=4 GOTO 3560
3470 IF I=6 GOTO 3570
3480 A(A+M1,B+M2)=6:A(A,B)=0: GOTO 3600
3490 X=INT(RND*8)+1:M1=C(X*2-1):M2=C(X*2)
3500 IF A+M1>0 IF A+M1<21 IF B+M2>0 IF B+M2<21 GOTO 3420
3510 GOTO 3490
3520 PRINT "SUB EATEN BY MONSTER!": GOTO 3790
3530 IF RND>.2 GOTO 3490
3540 PRINT "SHIP EATEN BY A SEA MONSTER":S=S-1: IF S=0 GOTO 3820
3550 GOTO 3480
3560 PRINT "HEADQUARTERS EATEN BY MONSTER":S3=0:S4=0:D2=0: GOTO 3480
3570 IF RND<.75 GOTO 3490
3580 PRINT "SEA MONSTERS FIGHT...": IF RND>.8 PRINT "...AND ONE DIES": GOTO 3480
3590 PRINT "...AND THEY TIE": GOTO 3490
3600 NEXT : NEXT : FOR A=1 TO 9:B=INT(RND*9)+1
3610 I=1: IF (D-51)*(D-2000)>0 LET I=I+1
3620 J=1: IF D(B)>=3 LET J=0
3630 D(B)=D(B)+RND*(2+RND*2)*I*J
3640 NEXT : GOTO 500
3650 REM
3660 REM  *COURSE/DIRECTION*
3670 REM
3680 PRINT TAB(14);"8 1 2"
3690 PRINT TAB(14);" \'/"
3700 PRINT TAB(14);"7-*-3"
3710 PRINT TAB(14);" /'\"
3720 PRINT TAB(14);"6 5 4"
3730 PRINT : INPUT "COURSE"C1: IF (C1-1)*(C1-8)>0 GOTO 3730
3740 X1=C(2*INT(C1+.5)-1):Y1=C(2*INT(C1+.5)): RETURN 
3750 EXIT 3790
3760 REM
3770 REM  *DESTRUCTION*
3780 REM
3790 PRINT : PRINT S;" ENEMY SHIPS STILL REMAIN"
3800 PRINT : INPUT "ANOTHER GAME"A$: IF MID$(A$,1,1)="Y" PRINT : GOTO 130
3810 END
3820 PRINT "    -- ALL SHIPS DESTROYED --": GOTO 3800

