
10 CLS: PRINT TAB(13);"KINGDOM": PRINT : PRINT : PRINT 
20 DEFINT Z
30 A=RND(99)
40 B=0
50 C=B
60 D=RND(900)+99:D1=D
70 F=6
80 L=400+2*A
90 K=25
100 G=D+L*2:G1=G
110 M=B
120 P=L+A
130 R=B
140 S=2*A
150 T=25
160 U=100
170 V=B
180 W=5
190 X=B
200 Y=1980
210 Z=B
220 V=V+RND(5)+(L/40*K/9+D/15+999/U-2*A-B/2+R)/9
230 IF V<-9 LET V=0
240 IF V<=0 GOTO 270
250 PRINT "HUN ATTACK ";U;" MILE";: IF V>1 PRINT "S";
260 PRINT " AWAY"
270 IF U<1 GOTO 1190
280 R=(R-W*F)/2+Z+12
290 IF R>0 PRINT "ARMY PLOTS A COUP"
300 IF R>1 LET C=C+R
310 IF C>50 GOTO 1190
320 Z=Z+RND(2)+P/L+75/(101-T)-S*21/P+5/F
330 IF Z>0 PRINT "PEASANT RIOTS"
340 IF Z<-3 LET Z=-3
350 M=M+1: IF M>12 LET M=1:Y=Y+1
360 RESTORE: FOR H=1 TO M: READ C$: NEXT 
370 DATA "JANUARY","FEBRUARY","MARCH","APRIL","MAY","JUNE","JULY"
380 DATA "AUGUST","SEPTEMBER","OCTOBER","NOVEMBER","DECEMBER"
390 PRINT : PRINT 1;" ";C$;" ";Y;":"
400 PRINT "GOLD = ";: IF D<0 PRINT "-";
410 PRINT "$";ABS(D),:E=(D-D1)*100/D1: IF E>=0 PRINT "+";
420 PRINT E;"%": IF D<0 GOTO 1190
430 PRINT "FOOD = ";: IF G<0 PRINT "-";
440 PRINT "#";ABS(G),:E=(G-G1)*100/G1: IF E>=0 PRINT "+";
450 PRINT E;"%": IF G<0 GOTO 1190
460 PRINT : PRINT "POPULATION    = ";A+P+S
470 PRINT "1.  PEASANTS  = ";P
480 PRINT "2.  SOLDIERS  = ";A+S
490 PRINT "3.  AT BATTLE = ";A
500 PRINT "4.  ";L;" ACRES OF LAND @ $";K
510 PRINT "5.  FED       = #";F
520 PRINT "6.  TAXES     = ";T;"%"
530 PRINT "7.  ARMY PAY  = $";W
540 PRINT "8.  HANG ANY?"
550 PRINT "9.  BRIBE     = $";B
560 PRINT : INPUT "YOUR COMMAND"Q
570 IF Q<0 GOTO 560
580 IF Q>10 GOTO 390
590 PRINT : IF Q>0 GOSUB 600+Q*60: GOTO 560
600 G1=G:G=G+L/4/(L*9/P+P*9/L)*M*(RND(4)-Z)-(A+P+S)/19*F
610 IF F<=3 PRINT P/9/F;" PEOPLE STARVED":P=P-P/9/F
620 IF V>0 LET A=A-V
630 U=U-V+A: IF U>99 LET U=100
640 IF A<1 LET A=0
650 K=K+RND(2)-1:D1=D:D=D-(A+S)*W+P/20*T-B:P=P+RND(9): GOTO 220
660 E=Z+RND(4): IF Z>0 PRINT " ";E;
670 IF Z<1 PRINT " ?";
680 PRINT " PROTESTOR";: IF E<>1 PRINT "S";
690 PRINT : INPUT " SHOOT ANY"Q: IF Q>Z IF Q>=P PRINT : PRINT "YOU CAN'T SHOOT EVERYBODY!": GOTO 690
700 Z=Z-Q:P=P-Q: IF P<1 LET P=1
710 RETURN 
720 PRINT " ARMY NOW ";A+S;" STRONG"
730 INPUT " +DRAFT, -DISCHARGE"Q
740 IF Q>P LET Q=P
750 IF Q<-S LET Q=-S
760 P=P-Q:S=S+Q
770 RETURN 
780 S=S+A: PRINT " ";A;" SOLDIER";
790 IF A>1 PRINT "S";
800 PRINT " NOW AT BATTLE": INPUT " NEW NUMBER"A
810 IF A<0 LET A=0
820 IF A>S LET A=S
830 S=S-A: RETURN 
840 PRINT " ";L;" ACRES NOW OWNED"
850 INPUT " +BUY,-SELL"Q
860 L=L+Q
870 D=D-Q*(K+Q/5)
880 K=K+Q/10
890 RETURN 
900 PRINT " PEOPLE NOW FED #";F
910 INPUT " NEW FOOD ALLOWANCE"F
920 IF F<1 LET F=1
930 RETURN 
940 REM  INSERTED TO ALLOW
950 REM  FOR COMP BR
960 PRINT " ";T;"% TAX NOW LEVIED"
970 INPUT " NEW TAX RATE"T
980 IF T>99 LET T=100
990 IF T<1 LET T=1
1000 RETURN 
1010 REM
1020 PRINT " SOLDIERS NOW PAID $";W
1030 INPUT " NEW WAGE"W
1040 IF W<0 LET W=0
1050 REM
1060 REM
1070 RETURN 
1080 PRINT " ?";: IF R>0 PRINT CHR$(8);R;
1090 PRINT " SOLDIER";: IF R<>1 PRINT "S";
1100 PRINT " PLOT";: IF R=1 PRINT "S";
1110 PRINT " A COUP": INPUT " HANG"Q: IF Q>S LET Q=S
1120 S=S-Q:R=R-Q: IF Q>1 LET C=C+1
1130 RETURN 
1140 PRINT " HUNS NOW BRIBED $";B
1150 INPUT " NEW BRIBE"B
1160 IF B<0 LET B=0
1170 REM
1180 RETURN 
1190 PRINT "YOU LOSE!"
1200 PRINT : PRINT TAB(9);"END GAME STATUS": PRINT 
1210 PRINT " LASTED ";Y-1980;" YEAR";: IF Y<>1981 PRINT "S";
1220 PRINT " ";M-1;" MONTH";: IF M<>2 PRINT "S";
1230 PRINT : PRINT " ";L;" ACRES OF LAND AT $";K
1240 PRINT : PRINT " GOLD = ";: IF D<0 PRINT "-";
1250 PRINT "$";ABS(D);TAB(16);"FOOD = ";: IF G<0 PRINT "-";
1260 PRINT "#";ABS(G)
1270 PRINT " ";P;" PEASANTS";TAB(16);"FED #";F
1280 PRINT " TAXED ";T;"%";TAB(16);"$";B;" BRIBE"
1290 PRINT " ";S;" IN POLICE";TAB(16);A;" IN BATTLE"
1300 PRINT " PAID $";W
1310 PRINT : PRINT " ";
1320 IF R>0 PRINT R;" PLOT COUP";TAB(16);
1330 IF Z>0 PRINT Z;" PROTESTOR";: IF Z>1 PRINT "S";
1340 PRINT : IF V>0 PRINT " ATTACK AT ";U;" MILES"
1350 PRINT : INPUT "ANOTHER GAME"C$
1360 IF MID$(C$,1,1)="Y" CLS: GOTO 20

