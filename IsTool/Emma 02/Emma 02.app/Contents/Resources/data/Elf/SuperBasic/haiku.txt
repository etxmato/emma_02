
10 REM *HAIKU-GENERATOR*
20 REM  SEE 'EXPERIMENTS IN
30 REM  ARTIFICIAL INTELLIGENCE
40 REM  FOR SMALL COMPUTERS'
50 REM  BY JOHN KRUTCH
60 CLD:L$=""
70 GOSUB 160
80 GOTO 100
90 GOSUB 240
100 GOSUB 290
110 IF R=1 GOSUB 500
120 IF R=2 GOSUB 710
130 IF R=3 GOSUB 920
140 IF R=4 GOSUB 1130
150 GOTO 90
160 CLS
170 DEFINT Z
180 A1=4
190 A2=50
200 N1=50
210 V1=14
220 P1=6
230 RETURN 
240 L$=""
250 RESTORE
260 INPUT A$: IF A$="QUIT" END
270 CLS
280 RETURN 
290 R=RND(4)+1
300 RETURN 
310 FOR I=1 TO LEN(L$)-2
320 IF MID$(L$,I,3)<>" A " GOTO 360
330 B$=MID$(L$,I+3,1)
340 IF B$<>"A" IF B$<>"E" IF B$<>"I" IF B$<>"O" IF B$<>"U" GOTO 360
350 L$=MID$(L$,1,I+1)+"N"+MID$(L$,I+2)
360 NEXT I
370 FOR I=1 TO LEN(L$)-2
380 IF MID$(L$,I,4)<>" AN " GOTO 410
390 B$=MID$(L$,I+4,1)
400 IF B$<>"A" IF B$<>"E" IF B$<>"I" IF B$<>"O" IF B$<>"U"L$=MID$(L$,1,I+1)+MID$(L$,I+3)
410 NEXT I
420 RETURN 
430 R=RND(N+1)+P
440 FOR I=1 TO R
450 READ W$
460 NEXT I
470 L$=L$+" "+W$
480 RESTORE
490 RETURN 
500 N=A1:P=0: GOSUB 430
510 N=A2:P=A1: GOSUB 430
520 N=N1:P=A1+A2: GOSUB 430
530 GOSUB 310
540 L$=L$+"..."
550 PRINT L$
560 L$=""
570 N=A1:P=0: GOSUB 430
580 N=N1:P=A1+A2: GOSUB 430
590 N=V1:P=A1+A2+N1: GOSUB 430
600 N=P1:P=A1+A2+N1+V1: GOSUB 430
610 N=A1:P=0: GOSUB 430
620 N=N1:P=A1+A2: GOSUB 430
630 GOSUB 310
640 PRINT L$
650 L$=""
660 N=A2:P=A1: GOSUB 430
670 GOSUB 430
680 N=N1:P=A1+A2: GOSUB 430
690 PRINT L$
700 RETURN 
710 N=N1:P=A1+A2: GOSUB 430
720 N=P1:P=A1+A2+N1+V1: GOSUB 430
730 N=A1:P=0: GOSUB 430
740 N=N1:P=A1+A2: GOSUB 430
750 GOSUB 310
760 L$=L$+";"
770 PRINT L$
780 L$=""
790 N=A1:P=0: GOSUB 430
800 N=A2:P=A1: GOSUB 430
810 N=N1:P=A1+A2: GOSUB 430
820 N=P1:P=A1+A2+N1+V1: GOSUB 430
830 N=A1:P=0: GOSUB 430
840 N=N1:P=A1+A2: GOSUB 430
850 GOSUB 310
860 PRINT L$
870 L$=""
880 N=A2:P=A1: GOSUB 430
890 N=N1:P=A1+A2: GOSUB 430
900 PRINT L$
910 RETURN 
920 N=A1:P=0: GOSUB 430
930 N=A2:P=A1: GOSUB 430
940 GOSUB 430
950 N=N1:P=A1+A2: GOSUB 430
960 GOSUB 310
970 L$=L$+";"
980 PRINT L$
990 L$=""
1000 N=P1:P=A1+A2+N1+V1: GOSUB 430
1010 N=A1:P=0: GOSUB 430
1020 N=A2:P=A1: GOSUB 430
1030 N=N1:P=A1+A2: GOSUB 430
1040 GOSUB 310
1050 PRINT L$
1060 L$=""
1070 N=A1:P=0: GOSUB 430
1080 N=N1:P=A1+A2: GOSUB 430
1090 N=V1:P=A1+A2+N1: GOSUB 430
1100 GOSUB 310
1110 PRINT L$
1120 RETURN 
1130 N=A1:P=0: GOSUB 430
1140 N=A2:P=A1: GOSUB 430
1150 N=N1:P=A1+A2: GOSUB 430
1160 N=V1:P=A1+A2+N1: GOSUB 430
1170 GOSUB 310
1180 L$=L$+";"
1190 PRINT L$
1200 L$=""
1210 N=A1:P=0: GOSUB 430
1220 N=A2:P=A1: GOSUB 430
1230 GOSUB 430
1240 N=N1:P=A1+A2: GOSUB 430
1250 GOSUB 310
1260 PRINT L$
1270 L$=""
1280 N=P1:P=A1+A2+N1+V1: GOSUB 430
1290 N=A1:P=0: GOSUB 430
1300 N=A2:P=A1: GOSUB 430
1310 N=N1:P=A1+A2: GOSUB 430
1320 GOSUB 310
1330 PRINT L$
1340 RETURN 
1350 DATA "A","THE","AN","THE"
1360 DATA "AUTUMN","HIDDEN","BITTER","MISTY","SILENT","EMPTY"
1370 DATA "DRY","DARK","SUMMER","ICY","DELICATE","QUIET"
1380 DATA "WHITE","COOL","SPRING","WINTER","DAPPLED"
1390 DATA "TWILIGHT","DAWN","CRIMSON","WISPY","AZURE"
1400 DATA "BLUE","BILLOWING","BROKEN","COLD","DAMP","FALLING"
1410 DATA "FROSTY","GREEN","LONG","LATE","LINGERING","LIMPID"
1420 DATA "LITTLE","MORNING","MUDDY","OLD","RED","ROUGH"
1430 DATA "STILL","SMALL","SPARKLING","THROBBINB","VERMILION"
1440 DATA "WANDERING","WITHERED","WILD","BLACK","YOUNG"
1450 DATA "WATERFALL","RIVER","BREEZE","MOON"
1460 DATA "RAIN","WIND","SEA","MORNING","SNOW","LAKE","SUNSET"
1470 DATA "PINE","SHADOW","LEAF","DAWN","GLITTER","FOREST"
1480 DATA "HILL","CLOUD","MEADOW","SUN","GLADE","BIRD","BROOK"
1490 DATA "BUTTERFLY","BUSH","DEW","DUST","FIELD","FIR"
1500 DATA "FLOWER","FIREFLY","FEATHER","GRASS","HAZE","MOUNTAIN"
1510 DATA "NIGHT","POND","SHADE","SNOWFLAKE"
1520 DATA "SILENCE","SOUND","SKY","SHAPE","SURF","THUNDER"
1530 DATA "VIOLET","WATER","WILDFLOWER","WAVE"
1540 DATA "SHAKES","DRIFTS","HAS STOPPED","STRUGGLES"
1550 DATA "HAS FALLEN","HAS PASSED","SLEEPS","CREEPS"
1560 DATA "FLUTTERS","HAS RISEN","IS FALLING","IS TRICKLING"
1570 DATA "MURMURS","IS FLOATING"
1580 DATA "ON","IN","OF","UNDER","OVER","NEAR"

