
10 CLS: PRINT TAB(7);"BIOMETRIC ANALYSIS": PRINT : PRINT : PRINT 
20 REM
30 REM  *INSTRUCTIONS*
40 REM
50 INPUT "WOULD YOU LIKE INSTRUCTIONS"R$: IF MID$(R$,1,1)<>"Y" GOTO 130
60 PRINT : PRINT : PRINT "     THIS PROGRAM COMPUTES YOUR LIFE EXPECTANCY.  IT ASKS YOU A"
70 PRINT "SERIES OF SHORT QUESTIONS.  YOU REPLY BY TYPING IN THE CORRE-"
80 PRINT "SPONDING ANSWER.  FOR EXAMPLE:  ": PRINT "DO YOU LIKE COMPUTERS?            G = YES"
90 PRINT "  I = NO": PRINT "YOUR CHOICE?  [YOU TYPE 'G']": WAIT(1000)
100 REM
110 REM  *INITIALIZATION*
120 REM
130 Z=72:B=60
140 A$="ABCDEMGHIJKLFNO"
150 REM
160 REM  *MAIN LOOP*
170 REM
180 FOR L=1 TO 20: PRINT 
190 FOR M=1 TO 7: READ X$
200 IF ASC(X$)=32 EXIT 250
210 IF ASC(X$)=64 PRINT : PRINT : PRINT TAB((31-LEN(X$))/2);MID$(X$,2): PRINT :M=M-1: GOTO 240
220 IF M<>1 PRINT "  ";
230 PRINT X$
240 NEXT 
250 PRINT "  ";MID$(X$,2): READ X$
260 REM
270 REM  *USER INPUT*
280 REM
290 INPUT "YOUR CHOICE"R$
300 FOR M=1 TO LEN(X$): IF MID$(X$,M,1)=MID$(R$,1,1) EXIT 350
310 NEXT : GOTO 290
320 REM
330 REM  **CALCULATE EFFECT*
340 REM
350 FOR M=1 TO 15: IF MID$(A$,M,1)=MID$(R$,1,1) EXIT 370
360 NEXT 
370 Z=Z+M-9: NEXT 
380 REM
390 REM  *RESULT PRINTOUT*
400 REM
410 CLS: PRINT "LIFE EXPECTANCY: ";Z;" YEARS"
420 IF Z<B END
430 FOR L=B TO Z STEP 5: READ X,Y: NEXT 
440 PRINT "OUT LIVING: ";X;"% OF MEN": PRINT "            ";Y;"% OF WOMEN"
450 REM
460 REM  *QUESTIONS/ANSWERS*
470 REM
480 DATA "@SEX","ARE YOU MALE OR FEMALE?","M = MALE"," F = FEMALE","MF"
490 DATA "@LIFESTYLE","WHERE DO YOU LIVE?","G = LARGE CITY","K = TOWN OR FARM"
500 DATA " I = NEITHER","GKI"
510 DATA "HOW DO YOU WORK?","M = BEHIND A DESK","L = HEAVY PHYSICAL LABOR"
520 DATA " I = NEITHER","MLI"
530 DATA "HOW OFTEN DO YOU STRENUOUSLY    EXERCISE?","F = FIVE TIMES A WEEK"
540 DATA "K = TWO OR THREE TIMES A WEEK"," I = LESS OFTEN","FKI"
550 DATA "WITH WHOM DO YOU LIVE?","N = SPOUSE, FAMILY, OR FRIEND"
560 DATA "H = ALONE FOR 10 YEARS SINCE        AGE 25","G = 11-20 YEARS","M = 21-30 YEARS"
570 DATA "E = 31-40 YEARS"," D = MORE THAN 40 YEARS","NGHMED"
580 DATA "@PSYCHE","DO YOU SLEEP MORE THAN 10 HOURS A NIGHT?","E = YES"," I = NO","IE"
590 DATA "@MENTAL STATE","WHAT SORT OF PERSONALITY DO YOU HAVE?","M = INTENSE, AGGRESSIVE"
600 DATA "L = EASY-GOING, RELAXED"," I = NEITHER","MLI"
610 DATA "@FEELINGS","HOW DO YOU GENERALLY FEEL?","J = HAPPY","G = UNHAPPY"," I = NEITHER","JGI"
620 DATA "@EXTERNAL FACTORS","HAVE YOU HAD A SPEEDING TICKET  DURING THE PAST YEAR?"
630 DATA "H = YES"," I = NO","HI"
640 DATA "@INCOME","IS YOUR SALARY ABOVE $50,000?","G = YES"," I = NO","GI"
650 DATA "@EDUCATION","WHAT IS THE HIGHEST LEVEL OF    EDUCATION YOU ACHIEVED?"
660 DATA "L = GRADUATE OR PROFESSIONAL        DEGREE","J = OTHER COLLEGE DEGREE"
670 DATA " I = NEITHER","JLI"
680 DATA "@AGE","ARE YOU OVER AGE 65 AND STILL   WORKING?","L = YES"," I = NO","LI"
690 DATA "@HEREDITY","HAVE...","K = GRANDPARENT(S) LIVED TO         85?"
700 DATA "O = ALL GRANDPARENTS LIVED TO       80?"," I = NEITHER?","KOI"
710 DATA "HAS ANY PARENT DIED OF A STROKE OR HEART ATTACK BEFORE AGE 50?","E = YES"
720 DATA " I = NO","EI"
730 DATA "@FAMILY DISEASES"
740 DATA "HAS ANY CLOSE RELATIVE HAD CAN- CER, A HEART CONDITION, OR DIA- BETES?"
750 DATA "M = YES"," I = NO","MI"
760 DATA "@HEALTH","HOW MUCH DO YOU SMOKE?","A = MORE THAN TWO PACKS A DAY"
770 DATA "C = ONE TO TWO PACKS","M = ONE-HALF TO ONE PACK"," I = NOTHING","ACMI"
780 DATA "@DRINKING","DO YOU DRINK THE EQUIVALENT OF AQUARTER BOTTLE OF LIQUOR A DAY?"
790 DATA "H = YES"," I = NO","HI"
800 DATA "@WEIGHT","ARE YOU...","A = OVERWEIGHT BY MORE THAN 50      POUNDS?"
810 DATA "E = OVER BY 30-50 POUNDS?","G = OVER BY 10-30 POUNDS?"," I = NOT OVERWEIGHT?","AEGI"
820 DATA "@MEDICINE","DO YOU...","K = IF A MALE OVER 40, HAVE AN      ANNUAL CHECKUP?"
830 DATA "K = IF A WOMAN, SEE A GYNE-         COLOGIST ONCE A YEAR?"," I = NEITHER?","KI"
840 DATA "@CURRENT AGE","HOW OLD ARE YOU?","N = OVER 70 YEARS","F = 50-70 YEARS"
850 DATA "L = 40-50 YEARS","K = 30-40 YEARS"," I = UNDER 30 YEARS","NFLKI"
860 REM
870 REM  *POPULATION DATA*
880 REM
890 DATA 26,15,36,20,48,30,61,39,75,53,87,70,96,88,99.9,99.6

