
2 REM *FETCH*
4 REM  SOLVES THE HYPOTHETICAL
6 REM  SYLLOGISM
10 REM
11 REM  *SUPERVISOR*
12 REM
20 GOSUB 110
30 GOSUB 170
40 GOSUB 210
50 IF X=1 GOSUB 570
60 IF X=2 GOSUB 850
70 IF X=3 GOSUB 310
80 GOTO 30
95 REM
100 REM  *INITIALIZATION*
105 REM
110 CLS: CLD: DEFINT Z
120 FOR I=0 TO 10: FOR J=0 TO 10
130 A$(J+I*10)="X"
140 NEXT J: NEXT I
150 RETURN 
155 REM
160 REM  *INPUT*
165 REM
170 X=0
180 INPUT D$
190 IF D$="QUIT" END
192 X$=MID$(D$,LEN(D$))
194 IF X$<>"." IF X$<>"?" PRINT "NO PUNCTUATION.": GOTO 180
200 RETURN 
205 REM
206 REM  *CONTROL*
207 REM
210 D$=MID$(D$,1,LEN(D$)-1)
220 IF MID$(D$,1,3)="IS "X=1: GOTO 280
230 IF MID$(D$,1,8)="REQUEST "X=2: GOTO 280
240 FOR I=1 TO LEN(D$)
250 IF MID$(D$,I,4)=" IS "X=3: EXIT 280
260 NEXT I
270 X=4
280 RETURN 
290 REM
300 REM  *DECLARATIVE*
305 REM
310 E$=MID$(D$,1,I-1):F$=MID$(D$,I+4)
320 FOR I=1 TO 6
330 READ R$
335 L=LEN(R$)
340 IF MID$(E$,1,L)=R$ LET E$=MID$(E$,L+1)
350 NEXT I
360 RESTORE
370 FOR I=1 TO 6
380 READ R$
385 L=LEN(R$)
390 IF MID$(F$,1,L)=R$ LET F$=MID$(F$,L+1)
400 NEXT I
410 RESTORE
420 DATA " A "," AN "," THE ","A ","AN ","THE "
440 FOR I=0 TO 9
450 IF A$(I*10)=E$ EXIT 500
460 NEXT I
470 FOR I=0 TO 9
480 IF A$(I*10)="X" LET A$(I*10)=E$:A$((I+1)*10)="X":A$(1+I*10)=F$:A$(2+I*10)="X": EXIT 530
490 NEXT I
500 FOR J=1 TO 9
510 IF A$(J+I*10)="X" LET A$(J+I*10)=F$:A$(J+I+I*10)="X": EXIT 530
520 NEXT J
530 PRINT "OKAY."
540 RETURN 
550 REM 
560 REM  *INTERROGATIVE*
565 REM
570 D$=MID$(D$,4)
580 IF MID$(D$,1,2)="A "D$=MID$(D$,3)
590 IF MID$(D$,1,3)="AN "D$=MID$(D$,4)
600 IF MID$(D$,1,4)="THE "D$=MID$(D$,5)
610 FOR I=1 TO LEN(D$)
612 FOR J=1 TO 3
614 READ R$
615 L=LEN(R$)
616 IF MID$(D$,I,L)=R$ LET E$=MID$(D$,1,I-1):F$=MID$(D$,I+L): EXIT 625
620 NEXT J
622 GOTO 630
625 EXIT 660
630 RESTORE
640 NEXT I
650 PRINT "I DON'T UNDERSTAND.": GOTO 820
660 RESTORE
710 FOR I=0 TO 9
720 IF A$(I*10)=E$ EXIT 750
730 IF A$(I*10)="X" PRINT "I DON'T KNOW.": EXIT 820
740 NEXT I
750 FOR I=1 TO 9
760 FOR J=0 TO 9
770 IF A$(I+J*10)<>F$ GOTO 800
780 IF A$(J*10)=E$ PRINT "YES.": EXIT 815
790 IF A$(J*10)<>E$ LET F$=A$(J*10): EXIT 803
800 NEXT : NEXT : PRINT "I DON'T KNOW.": RETURN 
803 EXIT 804
804 GOTO 750
815 EXIT 820
820 RETURN 
830 REM
840 REM  *REQUEST*
845 REM
850 E$=MID$(D$,9)
870 FOR I=0 TO 9
880 IF A$(I*10)=E$ EXIT 910
890 NEXT I
900 PRINT "I HAVE NO DATA ON '";E$;"'.": GOTO 960
910 PRINT E$;" IS . . ."
920 FOR J=1 TO 9
930 IF A$(J+I*10)="X" EXIT 960
940 PRINT A$(J+I*10)
950 NEXT J
960 RETURN 

