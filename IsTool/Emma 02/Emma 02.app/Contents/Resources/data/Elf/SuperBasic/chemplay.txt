
10 CLS: PRINT TAB(12);"CHEMPLAY": PRINT : PRINT : PRINT 
20 INPUT "NUMBER OF PLAYERS"N1: IF (N1-2)*(N1-7)>0 GOTO 20
30 DIM S(58,2),P(N1,5)
40 PRINT : PRINT : PRINT "1 - CHEMICAL COMPANY": PRINT "2 - MERCHANT BANKER": PRINT 
50 B=0:C=B: FOR L=1 TO N1: PRINT "PLAYER ";L;
60 INPUT Q: IF Q<>1 IF Q<>2 GOTO 60
70 P(L,2)=Q*10:B=B+1: IF Q=1 LET B=B-1:C=C+1
80 NEXT : PRINT : IF C-B<1 PRINT "TOO MANY MERCHANT BANKERS": GOTO 40
100 FOR L=1 TO N1:P(L,1)=38:P(L,4)=0:P(L,5)=0: IF P(L,2)=10 LET P(L,3)=16: GOTO 120
110 P(L,3)=65-67*B/C
120 NEXT : CLS
140 DIM L(N1*2,4): FOR L=1 TO N1*2:L(L,1)=0: NEXT 
160 FOR L=1 TO 58: READ S(L,1):S(L,2)=0: NEXT 
170 DATA 31,21,7,81,11,32,6,5,61,33,71,22,51,82,5,34,12,41,72,35
180 DATA 6,13,5,62,14,73,42,83,36,23,5,15,52,63,43,24,4,1,7,5,25
190 DATA 7,5,2,7,5,6,3,53,7,6,16,5,6,54,44,5,26
195 REM  MAIN LOOP
200 P1=INT(RND*N1)
210 P1=P1+1: IF P1>N1 LET P1=1
220 CLS: PRINT TAB(12);"PLAYER ";P1: GOSUB 1010
230 D=INT(RND*6)+1:E=INT(RND*6)+1: PRINT "DIE ROLLS = ";D,E
240 M=P(P1,1):L=P(P1,1)+D+E
250 IF (37-M)*(38-L)<=0 INPUT "ALTERNATIVE PATH"Q$: IF ASC(Q$)=#59 LET L=L+11
260 IF (48-M)*(48-L)<0 LET L=L-48
270 IF (39-M)*(38-L)>0 GOTO 290
271 FOR N=1 TO N1*2: IF L(L,1)=0 GOTO 278
272 IF L(L,2)<>P1 GOTO 278
273 IF L(L,3)<=0 LET L(L,1)=0: GOTO 278
274 P(P1,3)=P(P1,3)-L(L,4):L(L,3)=L(L,3)-L(L,4)
275 IF L(L,1)>0 LET P(L(L,1),3)=P(L(L,1),3)+L(L,4)
278 NEXT 
279 PRINT "FISCAL YEAR DIVIDEND"
282 X=0: FOR L=1 TO 58: IF L(L,1)=P1 LET X=X+L(L,3)
283 NEXT 
285 P(P1,3)=P(P1,3)+(P(P1,4)+X)/5
288 PRINT "CASH ASSETS NOW $";P(P1,3);" MILLION"
290 IF L>58 LET L=L-58
300 P(P1,1)=L: GOSUB 4500: IF T>=10 GOTO 700
310 GOSUB 300+50*N: GOTO 900
350 RETURN 
400 PRINT "INCOME TAX..."
405 X=0: FOR L=1 TO N1*2: IF L(L,1)<>P1 IF L(L,2)<>P1 GOTO 415
410 X=X+L(L,3)
415 NEXT 
420 P(P1,3)=P(P1,3)-(P(P1,3)-X)/3
430 PRINT "CASH ASSETS NOW $";P(P1,3);" MILLION": RETURN 
450 PRINT "PROPERTY TAX..."
455 P(P1,3)=P(P1,3)-P(P1,4)/10
460 RETURN 
500 PRINT "CASH FLOW PROBLEM": RETURN 
550 PRINT "MARKETS..."
555 IF INT(P(P1,2)/10)=1 LET P(P1,3)=P(P1,3)+P(P1,5)/2: RETURN 
560 P(P1,3)=P(P1,3)+P(P1,5)/5: RETURN 
600 PRINT "BOARD MEETING..."
602 GOSUB 690
604 GOSUB 635+D
608 PRINT "YIELDS $";E
610 P(P1,3)=P(P1,3)+E: RETURN 
637 PRINT "TAX REBATE": RETURN 
638 PRINT "FREIGHT RATES DROP": RETURN 
639 PRINT "EXCHANGE RATE FLUCTUATES": RETURN 
640 PRINT "LAWSUIT WON": RETURN 
641 PRINT "SUCCESSFUL ADVERTISING CAMPAIGN": RETURN 
642 PRINT "WORKER PRODUCTIVITY INCREASES": RETURN 
643 PRINT "NEW PROCESS DEVELOPED": RETURN 
644 PRINT "UNEXPECTED EXPORT ORDER": RETURN 
645 PRINT "NEW PRODUCT APPLICATION": RETURN 
646 PRINT "NEW MARKET OPENED": RETURN 
647 PRINT "SHORTAGE RAISES PRICES": RETURN 
650 PRINT "BOARD MEETING..."
651 GOSUB 690
652 GOSUB 670+D
653 PRINT "COMPANY LOSES $";E
654 P(P1,3)=P(P1,3)-E: RETURN 
690 D=INT(RND*11)+2
691 IF R<1 LET E=.099*D-.188: RETURN 
692 IF R<4 LET E=.3*D+.4: RETURN 
693 IF R<8 LET E=.4*D+1.2: RETURN 
694 E=D/2+4: RETURN 
1000 REM  COMMAND
1010 PRINT : PRINT : PRINT 
1012 PRINT "0 - CONTINUATION"
1015 PRINT "1 - STATUS REPORT"
1020 PRINT "2 - PROPERTY DEVELOPMENT"
1030 PRINT "3 - BORROWING"
1040 PRINT : INPUT "YOUR COMMAND"Q: IF Q*(Q-3)>0 GOTO 1040
1050 IF Q=1 GOSUB 2000: GOTO 1010
1060 IF Q=2 GOSUB 3000: GOTO 1010
1065 IF Q=3 GOSUB 3500: GOTO 1010
1070 RETURN 
1990 REM  STATUS
2000 PRINT "PROPERTY";TAB(15);"VALUE","REVENUE"
2010 FOR L=1 TO 58: GOSUB 4500: IF P1=O GOSUB 4000: PRINT N$;N;TAB(15);V,R
2020 NEXT 
2030 PRINT "POSITION = ";P(P1,1)
2040 PRINT "MONEY    = ";P(P1,3)
2050 RETURN 
2290 REM  DEVELOPMENT
3000 INPUT "SPACE NUMBER"L: GOSUB 4500: IF T<10 PRINT "ADMINISTRATIVE SPACE": RETURN 
3010 GOSUB 4000: PRINT : PRINT N$;N
3020 IF P1<>O PRINT "NOT OWNED BY PLAYER ";P1: RETURN 
3030 IF E=2 PRINT "FULLY EXTENDED": RETURN 
3040 PRINT "EXTENSION COST = $";U;" MILLION"
3050 INPUT "NUMBER OF EXTENSIONS"Q: IF E+Q>2 PRINT "ONLY ONE MORE EXTENSION ALLOWED": GOTO 3050
3060 P(P1,3)=P(P1,3)-Q*U:P(P1,4)=P(P1,4)+Q*U:S(L,2)=S(L,2)+Q
3070 P(P1,5)=P(P1,5)-R: GOSUB 4000:P(P1,5)=P(P1,5)+R
3080 IF U<>6 RETURN 
3090 A=L:P(P1,2)=P(P1,2)+Q:P(P1,5)=0
3100 FOR L=1 TO 58: IF L<>A GOSUB 4000: IF P1=O LET P(P1,5)=P(P1,5)+R
3110 NEXT 
3120 L=X: GOSUB 4000:P(P1,5)=P(P1,5)+R: RETURN 
3490 REM  LOANS
3500 X=0: FOR L=1 TO N1*2: IF L(L,2)=P1 LET X=X+1
3510 NEXT : IF X>2 PRINT "NO MORE LOANS PERMISSIBLE": RETURN 
3520 INPUT "BANKER"Q: IF Q<>0 IF P(Q,2)<20 PRINT "NO SUCH BANKER EXISTS": RETURN 
3525 INPUT "AMOUNT"P
3530 IF Q>0 GOTO 3600
3540 IF P>3 PRINT "$3 MILLION LOAN LIMIT": GOTO 3525
3550 I=20:T=4: GOTO 3700
3600 IF P>4.8 PRINT "$4.8 MILLION LOAN LIMIT": GOTO 3525
3610 INPUT "INTEREST RATE"I: IF I>50 PRINT "50% INTEREST LIMIT": GOTO 3610
3620 INPUT "NUMBER OF INSTALMENTS"T: IF T>6 PRINT "6-INSTALMENT LIMIT": GOTO 3620
3700 L(L,1)=Q:L(L,2)=P1:L(L,3)=P+P*I/100:L(L,4)=L(L,3)/T
3705 P(P1,3)=P(P1,3)+P:P(Q,3)=P(Q,3)-P
3710 RETURN 
3990 REM  PROPERTY
4000 GOSUB 4500
4005 IF T<10 RETURN 
4010 GOSUB 4080+T*2
4020 V=V+E*U:N$=N$+" ":X=P(P1,2):Y=X-INT(X/10)*10
4030 IF U<>6 IF Y<>0 LET R=R*1.05: IF Y>1 LET R=R*1.05: IF Y=3 LET R=R*1.09
4040 RETURN 
4100 N$="INTERMEDIATE":U=5:V=2:R=.05: IF E>0 LET R=E/4
4110 RETURN 
4120 N$="STEAM CRACKER":U=6:V=6:R=P(P1,5)/5
4130 RETURN 
4140 N$="PLASTIC":U=4:V=2:R=.05: IF E>0 LET R=E/2
4150 RETURN 
4160 N$="PIPELINE":U=5:V=5:R=1+E
4170 RETURN 
4180 N$="TRANSPORT":U=5:V=5:R=1+E
4190 RETURN 
4200 N$="SOLVENTS":U=1:V=1:R=.05: IF E>0 LET R=1: IF E=2 LET R=1.8
4210 RETURN 
4220 N$="FIBRES":U=4:V=2:R=.05: IF E>0 LET R=E*1.6
4230 RETURN 
4240 N$="RUBBER":U=3: IF E=1 LET U=4
4250 V=2:R=.05: IF E>0 LET R=.5: IF E=2 LET R=1.5
4260 RETURN 
4500 P=S(L,1):T=INT(P/10)*10:N=P-T:A=S(L,2):O=INT(A/10)*10:E=A-O: RETURN 

