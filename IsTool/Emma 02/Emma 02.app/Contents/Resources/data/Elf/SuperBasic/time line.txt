
10 REM  WARM START @550
20 CLS: PRINT TAB(12);"TIME LINE"
30 N1=1: GOSUB 600: DIM D(O+1)
40 INPUT "HEADING"H$(N1): IF H$(N1)<>"DONE"N1=N1+1: GOTO 40
50 N1=N1-1: FOR L=1 TO O:D(L)=0:C$(L)="": NEXT 
60 CLS: PRINT "ENTER ELEMENTS:"
70 INPUT "MONTH"B
80 IF B<0 GOTO 260
90 INPUT "DATE"A
100 INPUT "YEAR"C
110 INPUT "COMMENT"C$: IF C$="" PRINT "REDO - ";: GOTO 70
120 IF S1+Y1>C IF B<13 IF A>0 IF A<32 GOTO 140
130 PRINT "ILLEGAL - ENTER AGAIN": GOTO 70
140 X=(C-S1)*12*M1+(B-1)*M1+1
150 FOR L=X TO X+M1-1
160 IF D(L)=0 EXIT 200
170 NEXT 
180 INPUT "NO ROOM AVAILABLE.  QUIT"C$: IF MID$(C$,1,1)<>"Y" PRINT : GOTO 70
190 END
200 D(L)=A:C$(L)=C$
210 IF M1=1 PRINT : GOTO 70
220 FOR L=X TO X+M1-2
230 FOR K=L+1 TO X+M1-1
240 IF D(K)<D(L)T=D(K):D(K)=D(L):D(L)=T:C$=C$(K):C$(K)=C$(L):C$(L)=C$
250 NEXT : NEXT : PRINT : GOTO 70
260 POUT : PRINT CHR$(#1B,#47,#1B,#41,#06);
270 FOR L=1 TO N1: PRINT TAB((80-LEN(H$(L)))/2);H$(L): NEXT 
280 PRINT : PRINT 
290 PRINT "SCALE = ";72/FNUM(M1)/16;" MONTHS PER INCH": PRINT 
300 PRINT CHR$(#1B,#41,#04,#9C,#08,#5E,#0F)
310 FOR L=1 TO O STEP 12*M1
320 RESTORE
330 FOR K=1 TO 12*M1 STEP M1: READ C$
340 FOR H=1 TO M1
350 E=L+K+H-2
360 GOSUB 530
370 IF D(E)=0 GOTO 510
380 PRINT CHR$(#1B,#4C,#07,0);
390 PRINT CHR$(#7E,#7E,#3C,#3C,#18,#18,#18,#18);
400 DATA "JANUARY","FEBRUARY","MAR	H","APRIL","MAY","JUNE"
410 DATA "JULY","AUGUST","SEPTEMBER","OCTOBER","NOVEMBER","DECEMBER"
420 PRINT " ";D(E);" ";C$;" ";S1+(E-1)/M1/12;":  ";
430 FOR J=1 TO LEN(C$(E))
440 GOSUB 540
450 IF J=1 GOTO 490
460 IF I=94J=J+1: GOSUB 540: GOTO 490
470 IF I>64 IF I<91I=I+32
480 PRINT CHR$(#7F,#7F,#7F);
490 PRINT CHR$(I);
500 NEXT 
510 PRINT : GOSUB 530: PRINT : NEXT 
520 NEXT : NEXT : TOUT : END
530 PRINT CHR$(#1B,#4C,#0F,0);: FOR J=1 TO 16: OUT (2,#FF): WAIT(D): NEXT : RETURN 
540 I=ASC(MID$(C$(E),J,1)): RETURN 
550 CLS: PRINT TAB(11);"WARM START"
560 PRINT : INPUT "FILE NUMBER"F: DLOAD C,F
570 GOSUB 650
590 GOTO 260
600 PRINT : PRINT : PRINT 
610 DEFINT Z:D=30
620 INPUT "STARTING YEAR"S1
630 INPUT "NUMBER OF YEARS"Y1
640 INPUT "NUMBER OF ELEMENTS PER MONTH"M1
650 O=Y1*12*M1: PRINT 
660 RETURN 

