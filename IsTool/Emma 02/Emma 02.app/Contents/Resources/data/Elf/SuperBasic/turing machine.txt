
10 CLS: PRINT TAB(9);"TURING MACHINE"
20 PRINT : PRINT : PRINT 
30 INPUT "DO YOU NEED INSTRUCTIONS"X$
40 IF MID$(X$,1,2)="YE" GOSUB 1210
50 DEFINT Z
60 REM
70 REM *PROGRAM PARAMETERS*
80 REM
90 REM  NUMBER OF STATES
100 M=2
110 REM  NUMBER OF SYMBOLS
120 N=2
130 REM  LENGTH OF TABLE
140 O=200
150 REM  RULE ARRAY
160 DIM R(M,N)
170 REM
180 REM *SETUP FOR ADDITION*
190 REM
200 B$=" "
210 REM  RULE TABLE
220 REM X-AXIS:  STATE NUMBERS
230 REM Y:  CONSECUTIVE SYMBOLS
240 REM
250 REM  SYMBOLS
260 DATA "1","+"
270 REM  "REPLACEMENT SYMBOL",
280 REM    INCREMENT STATE/POS
290 DATA " ",11
300 DATA "+",22
310 DATA "1",21
320 DATA "1",0
330 REM
340 REM *FILL ARRAYS*
350 REM
360 RESTORE
370 REM  READ SYMBOLS
380 FOR X=1 TO N
390 READ P$((M+1)*X)
400 NEXT X
410 REM  READ RULES
420 FOR X=1 TO M
430 FOR Y=1 TO N
440 READ P$(X*Y),R(X,Y)
450 NEXT Y: NEXT X
460 X=1
470 REM  FILL TAPE
480 FOR X=1 TO O
490 T$(X)=B$
500 NEXT X
510 REM
520 REM *GET USER STRINGS*
530 REM
540 CLS: INPUT "STARTING POSITION OF STRING"Y
550 IF Y<1 GOTO 540
560 IF Y>O GOTO 540
570 INPUT "STRING"X$
580 FOR X=1 TO LEN(X$)
590 T$(Y)=MID$(X$,X,1)
600 Y=Y+1: NEXT X
610 PRINT : INPUT "ANOTHER"X$
620 IF MID$(X$,1,2)="YE" GOTO 540
630 GOSUB 1130
640 REM
650 REM *START MACHINE*
660 REM
670 S=1
680 PRINT : INPUT "STARTING POSITION"P
690 IF P<1 GOTO 680
700 IF P>O GOTO 680
710 CLS
720 T$=T$(P)
730 REM
740 REM *FIND SYMBOL*
750 REM
760 Y=0: FOR X=1 TO N
770 IF P$((M+1)*X)=T$(P)Y=X
780 NEXT X
790 IF Y=0 PRINT "INVALID SYMBOL @ ";P: GOTO 1050
800 REM
810 REM *NEW SYMBOL*
820 REM
830 T$(P)=P$(S*Y)
840 PRINT "*****";TAB(26);"*****"
850 PRINT "*   *   POSITION = ";P;TAB(26);"*   *"
860 PRINT "* ";T$;" *  ---------------->  * ";T$(P);" *"
870 PRINT "*   *      STATE = ";S;TAB(26);"*   *"
880 PRINT "*****";TAB(26);"*****"
890 PRINT : PRINT 
900 REM
910 REM *CHANGE STATE/POSITION*
920 REM
930 X=INT(R(S,Y)/10):Y=R(S,Y)-X*10
940 IF X=0 PRINT "OPERATION HALTED": GOTO 1050
950 IF X=1S=S+1
960 IF S>M PRINT "STATE OVERFLOW": GOTO 1050
970 IF X=3S=S-1
980 IF S<1 PRINT "STATE UNDERFLOW": GOTO 1050
990 IF Y=0X=0: GOTO 970
1000 IF Y=1P=P+1
1010 IF P>O PRINT "POSITION OVERFLOW": GOTO 1050
1020 IF Y=3P=P-1
1030 IF P>=1 GOTO 720
1040 PRINT "POSITION UNDERFLOW"
1050 PRINT : PRINT : PRINT 
1060 GOSUB 1130
1070 PRINT : INPUT "AGAIN"X$
1080 IF MID$(X$,1,2)="YE" GOTO 460
1090 END
1100 REM
1110 REM *PRINT TAPE*
1120 REM
1130 PRINT : PRINT "THE TAPE:"
1140 FOR X=1 TO O
1150 PRINT T$(X);
1160 NEXT X
1170 RETURN 
1180 REM
1190 REM *INSTRUCTIONS*
1200 REM
1210 PRINT 
1220 PRINT "     THIS PROGRAM SIMULATES THE"
1230 PRINT "TURING MACHINE, A CONCEPT "
1240 PRINT "DEVELOPED BY BRITISH MATHEMATI-"
1250 PRINT "CIAN ALAN MATHISON TURING.  IN"
1260 PRINT "BRIEF, THE TURING MACHINE IS A"
1270 PRINT "HYPOTHETICAL COMPUTING DEVICE,"
1280 PRINT "WHICH OPERATES BY SCANNING A"
1290 PRINT "TAPE AND REPLACING SYMBOLS"
1300 PRINT "DEPENDING ON MACHINE 'STATE'."
1310 INPUT "CONTINUE"X$
1320 PRINT : PRINT "     THE PROGRAM FIRST ASKS YOU"
1330 PRINT "FOR STRINGS OF CHARACTERS TO BE"
1340 PRINT "PRINTED ON THE TAPE.  THEN IT"
1350 PRINT "BEGINS OPERATIONS.  ANY PAR-"
1360 PRINT "TICULAR TURING MACHINE HAS A"
1370 PRINT "SET OF RULES; THESE ARE LOCATED"
1380 PRINT "NEAR THE BEGINNING OF THE PRO-"
1390 PRINT "GRAM."
1400 PRINT : RETURN 
1410 PRINT "THE BEGINNING OF THE PROGRAM."
1420 PRINT "FIRSTLY, THE SYMBOL FOR A BLANK"
1430 PRINT "SPACE IS DEFINED, THEN THE LIST"

