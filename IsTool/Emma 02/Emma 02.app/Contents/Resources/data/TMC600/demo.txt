C
1 REM ESITTELYOHJELMA VER.2.1/JH;ENGLANNINKIELINEN
5 GOTO 600
8 REM SINI-JA EXPONENTTIKÄYRÄT
10 CURSOR 1: CLS
20 FOR X=0 TO 79 STEP 2: COLOR 4:A=PLOT(X,36+30*SIN(0+X/10))
30 COLOR 2:A=PLOT(X+1,71-EXP(0+X/19) ): NEXT
35 COLOR 5: MOVE(13, 6)"SIN": MOVE(21, 16)"EXP": CURSOR 0: WAIT(600):GOTO 600
38 REM VÄRIPALKIT
40 CLS: CURSOR 1: PRINT "     CHARACTER COLORS"
45 A=0:B=23:C=0:D=191
50 FOR B=23 TO 5 STEP -1
55 COLOR C: MOVE(A,B)CHR$(D)
60 NEXT :A=A+2:C=C+1
62 IF C=8D=#7F
65 IF C=16 WAIT(700): GOTO 600
66 GOTO 50
68 REM SBASIC KÄSKYT(KÄYTTÄJÄN KONEESSA OLEVAT)
70 CLS: PRINT "SBASIC COMMANDS(WITHOUT EXPANSION ROM 2)"
75 PRINT : HELP : PRINT : INPUT "ENTER M FOR MENU"B$
80 IF B$="M" GOTO 600
85 GOTO 75
195 REM ÄÄNITESTI
200 CLS: PRINT TAB(10);"SOUND OUTPUT"
205 PR:PR"1.Single frequency tones in 8 setectableranges,with 128 different tones in each."
210 PRINT "2.White-noise output in 8 selectable ranges,with the white-noise consistins of"
215 PRINT "all 128 tones of each range."
230 A=1:C=1
235 FOR B=0 TO 254 STEP 2
240 H=BEEP(B,C,16)
242 WAIT(30)
245 NEXT
246 IF A=2I=WNOISE(0,0):H=BEEP(0,0,0): GOTO 600
250 I=1: COLOR 3: PRINT : PRINT " WHITE-NOISE"
255 I=WNOISE(5,5)
260 A=A+1 
265 GOTO 235
295 REM TILASTO-OHJELMA:300-405 X/Y JANOJEN JA SKAALAN PIIRTO 
300 CURSOR 1: CLS
305 A=6
310 FOR B=0 TO 18 STEP 2
315 MOVE(A,B)CHR$(198)
320 NEXT
325 FOR B=1 TO 19 STEP 2
330 MOVE(A, B)CHR$(194)
335 NEXT
340 B=20
345 FOR A=7 TO 31 STEP 2
350 MOVE(A,B)CHR$(192)
355 NEXT
360 FOR A=8 TO 32 STEP 2
365 MOVE(A,B)CHR$(198)
370 NEXT
375 D=1:A=3:B=18:C=20: COLOR 3
380 FOR B=18 TO 0 STEP -2
390 MOVE(A, B)C:C=C+20
395 D=D+1: IF D=5A=2
400 NEXT
402 REM PYLVÄÄN KORKEUS JA VÄRI SATUNNAISLUKUINA
405 A=6
415 A=A+1: IF A=33 GOTO 450
420 X=RND(20):C=RND(8): COLOR C
422 FOR B=19 TO X STEP -1
425 MOVE(A, B)CHR$(191)
430 NEXT
435 GOTO 415
450 COLOR 5: MOVE(0, 23)"1=CONTINUE, 2=MENU"
455 INPUT A$
460 IF A$="1" MOVE(19,23)"   ": GOTO 480
465 IF A$="2" GOTO 600
470 GOTO 450
475 GOTO 455
478 REM TAUSTAVÄRIN VAIHTO
480 FOR A=7 TO 0 STEP -1
485 BKG A: WAIT(500)
490 NEXT
495 GOTO 450
500 REM SUPER CALCULATOR
505 CLS: PRINT "This program demonstrates a simple but  powerfull CALCULATOR-program"
510 PRINT "It consists only three lines of SBASIC- language utilizing FVAL command :"
512 PRINT
515 COLOR 3: PRINT "10 INPUT A$: IF A$=0 GOTO 600"
520 PRINT "15 PR FVAL(A$)"
525 PRINT "20 GOTO 10"
530 PRINT : COLOR 5
535 PRINT "Enter any mathematical function of max. 48 characters(incl. operators). "
540 PRINT "Enter 0 for menu."
545 PRINT : COLOR 1
550 INPUT A$: IF A$="0" GOTO 600
555 COLOR 4: PRINT FVAL(A$): COLOR 5
560 GOTO 535
562 REM MERKKIGENERAATTORI
565 CLS:K=1
570 FOR H=#00 TO #FF
575 PRINT CHR$(H);
580 NEXT
585 K=K+1: WAIT(800): IF K=3 GOTO 600
590 PRINT : PRINT : INPUT "ENTER M TO CONTINUE"C$
595 IF C$="M" RES 1: COLOR 7: GOTO 570
596 GOTO 590
598 REM PÄÄOHJELMA(MENU)
600 RES 0: COLOR 5: CURSOR 0: GOTO 765
605 COLOR 5: PRINT
610 PRINT "  1.TEXT EDITOR","2.STATISTICS"
615 PRINT "  3.CALCULATOR","4.CHARACTER SET"
620 PRINT "  5.SOUND TEST","6.MATH CURVES"
625 PRINT "  7.COLORS", "8.COMMANDS"
645 CURSOR 1: MOVE(2,16)"THIS PROGRAM TAKES ONLY 4 KB OF RAM!"
647 REM ILMAN REM-LAUSEITA
650 GOSUB 700
655 BKG 0: CURSOR 0: COLOR 5
660 MOVE(2,21): INPUT "SELECT PROGRAM NUMBER"A$
665 IF A$="1" GOTO 895
670 IF A$="2" GOTO 300
675 IF A$="3" GOTO 500
680 IF A$="4" GOTO 565
685 IF A$="5" GOTO 200
686 IF A$="L" END
687 IF A$="6" GOTO 10
688 IF A$="7" GOTO 40
689 IF A$="8" GOTO 70
690 GOTO 660
695 KEHYKSENPIIR TO
700 CURSOR 1: COLOR 7: BKG 2
705 FOR A=0 TO 39
710 MOVE(A, 0)"*"
715 NEXT
720 FOR B=1 TO 23
725 MOVE(39, B)"*"
730 NEXT
735 FOR A=39 TO 0 STEP -1
740 MOVE(A, 23)"*"
745 NEXT
750 FOR B=22 TO 1 STEP -1
755 MOVE(0, B)"*"
760 NEXT : COLOR 4: MOVE(12,0)"RCA CDP1802A CPU":MOVE(12,23)"RCA CDP1802A CPU": COLOR 7: RETURN 
765 CLS: COLOR 7: PRINT : PRINT : PRINT TAB(5);"TELMAC MICROCOMPUTER SYSTEMS" 
770 PRINT : PRINT TAB(10);"TMC-600 COMPUTER" 
775 PRINT : COLOR 5: PRINT TAB(2);"EXAMPLE PROGRAMS:" 
780 GOTO 605
890 REM TEKSTIEDITORI ILMAN TALLENNUSTA 
895 CLS: GOSUB 950 
900 M=KEY
905 IF M=0 GOTO 900 
910 N=KEY
915 IF N=0 GOTO 900 
920 IF N=M WAIT(30) : GOTO 930
925 GOTO 900 
930 IF N=8 GOTO 600 
935 IF N=27 GOSUB 1100: GOTO 900 
940 PRINT CHR$(N); 
945 GOTO 900
950 PRINT "TEXT EDITOR PROGRAM" 
955 PRINT 
960 PRINT "You can create very easily files of yourown information."
965 PRINT "Written pages can be stored into RAM,   cassette or floppy discs."
970 PRINT "Try it!Enter ESC to change character    colors.Enter any color number (0 to 7)" 
975 PRINT "or SHIFT 0 to 7 for blinking colors." 
980 PRINT "Enter DEL for menu."
985 PRINT
990 PRINT "Enter X to start the program": INPUT A$
995 IF A$="X" CLS: RETURN 
1000 GOTO 990 
1100 M=KEY
1105 IF M=0 GOTO 1100 
1110 N=KEY
1115 IF N=0 GOTO 1100 
1120 IF N=M GOTO 1130 
1125 GOTO 1100 
1127 REM VÄRIVALINTA NÄPPÄIMISTÖLTÄ
1130 IF N=48 COLOR 0: RETURN
1135 IF N=49 COLOR 1: RETURN 
1140 IF N=50 COLOR 2: RETURN 
1145 IF N=51 COLOR 3: RETURN 
1150 IF N=52 COLOR 4: RETURN 
1155 IF N=53 COLOR 5: RETURN 
1160 IF N=54 COLOR G: RETURN 
1165 IF N=55 COLOR 7: RETURN 
1170 IF N=95 COLOR 8: RETURN 
1175 IF N=33 COLOR 9: RETURN 
1180 IF N=34 COLOR 10: RETURN 
1185 IF N=35 COLOR 11: RETURN 
1190 IF N=36 COLOR 12: RETURN 
1195 IF N=37 COLOR 13: RETURN 
1200 IF N=38 COLOR 14: RETURN 
1205 IF N=39 COLOR 15: RETURN 
1210 GOTO 1100
