	;  PROGRAM 7.6	-- TV DIGITAL CLOCK
	;
REST:	GHI 0	  ; INITIALIZE R1, R2, R3
	PHI 1
	PHI 2
	PHI 3
	LDI INTS  ; R1 = INTERRUPT PC
	PLO 1
	LDI 0FFH  ; R2 = STACK
	PLO 2
	LDI MAIN  ; R3 = MAIN PC
	PLO 3
	RET	  ; X=0!
	DB 023H	  ; SET X=2, P=3
MAIN:	INP 1	  ; TURN ON TV
	BR TEST	  ; DO DIAGNOSTIC

;  DISPLAY REFRESH

DONE:	INC 2	  ; ASSUME X=2!
	LDA 2	  ; RESTORE DF
	SHR
	LDA 2	  ; RESTORE R7
	PHI 7
	LDA 2
	PLO 7
	LDA 2	  ; NOW D
	RET	  ; RESTORE X AND P
INTS:	NOP	  ; EVEN OUT CYCLES
	DEC 2	  ; PUSH STACK, TO
	SAV	  ; SAVE X AND P (IN T)
	DEC 2
	STXD	  ; SAVE D
	GLO 7	  ; SAVE R7
	STXD
	GHI 7
	STXD
	SHLC	  ; SAVE DF
	STXD
	LDI BUFF  ; SET UP R0
	B1  $	  ; WAIT FOR DISPLAY
ROW:	PLO 0     ; DMA HERE
	PLO 0	  ; RESET R0
	PHI 7
	LDI 11	  ; (RASTER COUNT - 3)/2
	PLO 7
	GHI 7	  ; KEEP FIXING R0
	PLO 0
REPT:	DEC 7	  ; COUNTER RASTERS
	GHI 7
	PLO 0
	PLO 0
	GLO 7	  ; TWO LINES PER LOOP
	BNZ REPT
	GLO 0	  ; IF LAST TIME,
	BN1 ROW
	PLO 0	  ; JUST BLANK IT
	B1 $-1	  ; (3 LINES)

; l  SECONDS CLOCK

	GHI 0
	PHI 7
	LDI FRCT  ; POINT TO FRAME COUNT
	PLO 7	  ; R7 IS AVAILABLE
	LDN 7
	ADI 1	  ; BUMP COUNTER
	STR 7
	SMI 61	  ; MOD 61
	BNF DONE  ; NOT OVER
	SEX 7
	STXD	  ; ROLL OVER
	LDX	  ; TO SECONDS
	ADI 3
	STR 7
	BNF UNIT  ; GO DISPLAY
	LDI -30	  ; ROLL OVER
	STXD
	LDX	  ; TO TENS
	ADI 3
	STR 7
	ADI 12	  ; (OVERFLOW AT 60)
	BNF TENS
	LDI -30	  ; ONE MINUTE!
	STR 7     ; COULD DO MINUTES, HOURS;.
TENS:	LDI BUFF  ; POINT TO LEFT DIGIT
	BR UNIT+2
	LDI SECS  ; (POINT TO COUNTER)
	PLO 7
UNIT:	LDI BRIT  ; OR RIGHT DIGIT
	PLO 0
	LDA 7	  ; POINT TO DIGITS
	ADI TABL  ; (TABLE OFFSET)
	PLO 7
DOWN:	LDA 7	  ; GET DOTS
	STR 2	  ; (SAVE)
	SEX 2
HALF:	LDX	  ; CONVERT A DOT
	SHL	  ; FROM A BIT
	STR 2
	SDB	  ; =00 IF DF=1, =FF IF DF=0
	STR 0	  ; STORE INTO BUFFER
	INC 0
	GLO 0
	ANI 3	  ; DO THIS 4 TIMES
	BNZ HALF  ; (9*4 INSTRUCTIONS)
	INC 0
	INC 0
	INC 0
	INC 0
	LDX	  ; CHECK FOR SECOND 4 BITS
	BNZ HALF  ; ((36+6)*2)
	GLO 0	  ; REPEAT IF THIS WAS LEFT
	SMI BEND
	BNF DOWN  ; ((84+6)*3)
	BZ UNIT-3 ; ((270+9)*2)
	BR DONE	  ; MAX TOTAL <600 INSTRUCTIONS

;  DOT TABLE FOR DIGITS

	DB 0DAH
	DB 0AAH
	DB 0DFH	  ; 0
	DB 0D9H
	DB 0DDH
	DB 08FH	  ; 1
	DB 09EH
	DB 0DBH
	DB 08FH	  ; 2
	DB 09EH
	DB 0DEH
	DB 09FH	  ; 3
	DB 0EAH
	DB 0A8H
	DB 0EFH	  ; 4
	DB 08BH
	DB 09EH
	DB 09FH	  ; 5
	DB 0CBH
	DB 09AH
	DB 0DFH	  ; 6
	DB 08EH
	DB 0DBH
	DB 0BFH   ; 7
	DB 0DAH
	DB 0DAH
	DB 0DFH	  ; 8
	DB 0DAH
	DB 0CEH
	DB 0DFH	  ; 9


;  MINI DIAGNOSTIC
TABL:	
TEST:	SEX 7	  ; SET UP R7
	PLO 7	  ; AS COUNTER
	BNF PLUS
	INC 7
PLUS:	IRX
	ADCI 1	  ; IN PARALLEL WITH D
	BNZ PLUS
	SEX 2	  ; WHEN D=00,
	GLO 7	  ; COMPARE THEM:
	DEC 2
	STR 2
	OUT 4	  ; DISPLAY DIFFERENCE
	BZ $+4	  ; EQUAL CONTINUES,
	BN4 $	  ; UNEQUAL WAITS
	B4  $	  ; WAIT FOR RELEASE
	INP 4	  ; GET NEW COUNT
	SHL	  ; SET DF
	BR TEST	  ; REPEAT

;  TIME COUNTERS AND DISPLAY BUFFER

STEN:	DB 0E2H	  ; MUST INITIALIZE
SECS:	DB 0E2H
FRCT:	DB 000H

BUFF:	 EQU 0C8H	  ; EMPTY BUFFER
BRIT:	 EQU 0CCH
BEND:	 EQU 0F8H

